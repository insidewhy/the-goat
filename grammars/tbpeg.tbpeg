# special
Spacing <- \s^+
Grammar <- (Rule / TreeRule)+

# general
RuleName <= value:([A-Z] ^ [a-zA-Z0-9_]^+)
PropertyName <- [a-z_] ^ [a-zA-Z0-9_]^+

# rules
Rule <= name:RuleName "<-" expression:Expression

# main expression tree
Expression <- $next
Alternation <= expressions:$next |% "/"
Sequence <= expressions:($next / Predicate)|+
Assignment <= (propertyName:PropertyName ":")|? expression:$next
Join <=
  expression:$next (
    repetition:$enum("%+" $as OneOrMore / "%" $as ZeroOrMore)
    joinWith:$next
  )|?
Lexeme <= expressions:$next |% "^"
Repetition <= expression:$next repetition:$enum(
  "+" $as OneOrMore /
  "*" $as ZeroOrMore /
  "^+" $as LexemeOneOrMore /
  "^*" $as LexemeZeroOrMore /
)|?
ExpressionLeaf <-
  Group /
  RuleName &!"<" /
  Constant /
  AnyCharacter /
  EscapeCode /
  Enum /
  Characters /
  NotCharacter /
  Next

Predicate <= predicate:$enum("&!" $as NotPredicate / "&" $as AndPredicate) expression:ExpressionLeaf

Group <= "(" expression:Expression ")"

# expression leafs
String <= \" ^ value:(EscapeSequence / ! \")^+ ^ \"
EscapeSequence <= \\ ^ ( value:(\\ / "n" / \") )
SpacingRule <= "~"
Constant <- String / SpacingRule / EscapeSequence
Next <= "$next"
AnyCharacter <= "."
NotCharacter <= "!" character:(EscapeSequence / \" ^ . ^ \")
EscapeCode <= \\ ^ code:("w" / "s")
Enum <= "$enum" "(" expression:(EnumValAssignment %+ "/") ")"
EnumValAssignment <= match:Constant ~ "$as" ~ enumValue:[a-zA-Z_]+

# expression leaf for character range
Characters <= "[" ^ matches:(CharacterRange / EscapeCode / EscapeSequence / \w)+ ^ "]"
CharacterRange <= (from:\w) ^ "-" ^ (to:\w)

# Tree rules
TreeRule <= name:RuleName "<=" expression:TreeExpression
TreeExpression <- TreeRepetition / TreeJoin / TreeSequence

TreeRepetition <= expression:Expression "|+"
TreeJoin <= expression:Expression "|%" joinWith:Constant

TreeSequence <= expressions:(TreeOption / Assignment / Predicate)+
TreeOption <= option:Assignment "|?"
